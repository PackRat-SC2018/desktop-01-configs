
##########	GMail FVWM Script
##########	Google mail checker
##########	by sebaro
##########
##########	rev.2012-08-14

WindowTitle {GMail}
WindowSize 320 160
WindowPosition 954 177

##### Global Style
Colorset 10
Font "xft:DejaVu Sans:size=8:bold"

Init
Begin
  ##### Widgets Style
  Set $defaultColorset = {10}
  Set $warnColorset = {12}
  Set $emailColorset = {13}
  Set $titleFont = {xft:DejaVu Sans:size=11:bold}

  ##### Settings
  Set $homeDir = (GetOutput {echo $HOME} 1 -1) {/.fvwm/scripts/GMail/}
  Set $putTmp = {/dev/shm/}
  Set $ggUserName = {}
  Set $ggPassWord = {}
  Set $updateTime = {900}

  ##### Widgets
  Set $gmailIcon = $homeDir {icons/gmail.png}
  ChangeIcon 1 $gmailIcon
  ChangeFont 2 $titleFont
  Set $mailIcon = $homeDir {icons/mail.png}
  ChangeIcon 3 $mailIcon
  ChangeIcon 6 $mailIcon
  ChangeIcon 9 $mailIcon
  Set $goupButton = $homeDir {icons/up.png}
  ChangeIcon 12 $goupButton
  Set $godownButton = $homeDir {icons/down.png}
  ChangeIcon 13 $godownButton
  For $i = 3 To 13 Do
    HideWidget $i
  Set $quitButton = $homeDir {icons/quit.png}
  ChangeIcon 14 $quitButton

  ##### Data Fetch
  Set $fetchFile = $putTmp {gmail_fetch_file}
  Set $fetchFileStatus = $putTmp {gmail_fetch_file_status}
  Do {exec echo "" > "} $fetchFileStatus {"}
  Set $gMail = {https://} $ggUserName {:} $ggPassWord {@mail.google.com/mail/feed/atom}

  ##### Variables
  Set $doFetch = {true}

  ##### Curly Brackets
  Set $leftCB = (GetOutput {printf "\173"} 1 -1)
  Set $rightCB = (GetOutput {printf "\175"} 1 -1)
End

PeriodicTasks
Begin
  ##### Get Data
  If $doFetch == {true} Then
  Begin
    ChangeTitle 2 {Fetching messages...}
    Do {exec wget "} $gMail {" -O "} $fetchFile {" && (echo $(cat "} $fetchFile {") > "} $fetchFile {" && sed -i -e "s/<entry>/\n&/g" "} $fetchFile {" && echo "1" > "} $fetchFileStatus {") || echo "0" > "} $fetchFileStatus {"}
    Set $fetchReady = {false}
    Set $doFetch = {false}
  End

  ##### Fetching
  If $fetchReady == {false} Then
  Begin
    Set $getFetchStatus = {cat "} $fetchFileStatus {"}
    If (GetOutput $getFetchStatus 1 -1) == 1 Then
    Begin
      Do {exec echo "" > "} $fetchFileStatus {"}
      For $i = 3 To 11 Do
        ShowWidget $i
      ChangeColorset 2 $defaultColorset
      Set $fetchReady = {true}
      Set $action = {start}
      Set $doRefresh = {true}
    End
    If (GetOutput $getFetchStatus 1 -1) == 0 Then
    Begin
      Do {exec echo "" > "} $fetchFileStatus {"}
      For $i = 3 To 13 Do
        HideWidget $i
      Set $fetchReady = {true}
      ChangeColorset 2 $warnColorset
      ChangeTitle 2 {Fetching GMail Failed!}
    End
  End

  ##### Print Data
  If $doRefresh == {true} Then
  Begin
    ## First Page
    If $action == {start} Then
    Begin
      Set $getNrEMails = {grep "<fullcount>" "} $fetchFile {" | awk -F '<fullcount>' '} $leftCB {print $2} $rightCB { ' | awk -F '</fullcount>' '} $leftCB {print $1} $rightCB {' |sed -e "s/^$/ /" -e "s/^ *$/empty/"}
      Set $nrEMails = (GetOutput $getNrEMails 1 -1)
      If $nrEMails == {empty} Then
      Begin
        Set $getNrEMails = {cat "} $fetchFile {" | grep "<entry>" | wc -l}
        Set $nrEMails = (GetOutput $getNrEMails 1 -1)
      End
      If $nrEMails == 0 Then
        Set $newMess = {No new messages}
      If $nrEMails == 1 Then
        Set $newMess = {1 new message}
      If $nrEMails > 1 Then
        Set $newMess = $nrEMails { new messages}
      ChangeTitle 2 $newMess
      If $nrEMails > 0 Then
      Begin
        Set $getTitles = {grep "<entry>" "} $fetchFile {" | awk -F '<title>' '} $leftCB {print $2} $rightCB { ' | awk -F '</title>' '} $leftCB {print $1} $rightCB {' | sed -e "s/\`//g" -e "s/|//g" -e "s/\\$/\$ /g" -e "s/^$/ /" -e "s/^ *$/empty/"}
        Set $getEMails = {grep "<entry>" "} $fetchFile {" | awk -F '<email>' '} $leftCB {print $2} $rightCB { ' | awk -F '</email>' '} $leftCB {print $1} $rightCB {' | sed -e "s/^$/ /" -e "s/^ *$/empty/"}
      End
      Set $maxEMails = 20
      If $nrEMails > $maxEMails Then
        Set $nrEMails = $maxEMails
      Set $startEMail = 1
      HideWidget 12
      If $nrEMails > 3 Then
        ShowWidget 13
    End

    ## Next Page
    If $action == {next} Then
    Begin
      If $startEMail == 1 Then
        ShowWidget 12
      Set $startEMail = (Add $startEMail 3)
      Set $stopEMail = (Add $startEMail 2)
      For $i = $startEMail To $stopEMail Do
      Begin
        If $i == $nrEMails Then
          HideWidget 13
      End
    End

    ## Previous Page
    If $action == {prev} Then
    Begin
      For $i = $startEMail To $stopEMail Do
      Begin
        If $i == $nrEMails Then
          ShowWidget 13
      End
      Set $startEMail = (Add $startEMail -3)
      Set $stopEMail = (Add $startEMail 2)
      If $startEMail == 1 Then
        HideWidget 12
    End

    ## Print Page
    Set $titleWidget = 4
    Set $emailWidget = 5
    For $i = 1 To 3 Do
    Begin
      Set $currEMail = (Add $startEMail (Add $i -1))
      Set $thisTitle = (GetOutput $getTitles $currEMail -1)
      Set $findDecimalEntities = {echo "} $thisTitle {" | grep "&#"}
      If (GetOutput $findDecimalEntities 1 -1) <> {} Then
      Begin
        Set $replaceDecimalEntities = {perl -CS -e '$txt="} $thisTitle {";@entities=($txt=~/&#(.} $leftCB {2,4} $rightCB {);/g); foreach (@entities) } $leftCB {$char=chr($_);$txt=~s/&#(.} $leftCB {2,4} $rightCB {);/$char/} $rightCB {;print $txt'}
        Set $thisTitle = (GetOutput $replaceDecimalEntities 1 -1)
      End
      Set $findAlphaEntities = {echo "} $thisTitle {" | grep "&\(.\} $leftCB {2,4\} $rightCB {\);"}
      If (GetOutput $findAlphaEntities 1 -1) <> {} Then
      Begin
        Set $replaceAlphaEntities = {echo "} $thisTitle {" | sed -e "s/&amp;/\&/g" -e "s/&quot;/\"/g" -e "s/&lt;/</g" -e "s/&gt;/>/g"}
        Set $thisTitle = (GetOutput $replaceAlphaEntities 1 -1)
      End
      Set $thisEMail = (GetOutput $getEMails $currEMail -1)
      If $nrEMails == 0 Then
      Begin
        ChangeColorset $titleWidget $warnColorset
        ChangeTitle $titleWidget {No message}
        ChangeTitle $emailWidget {}
      End
      Else
      Begin
        If $currEMail > $nrEMails Then
        Begin
          ChangeColorset $titleWidget $warnColorset
          ChangeTitle $titleWidget {No more messages}
          ChangeTitle $emailWidget {}
        End
        Else
        Begin
          If $thisTitle == {empty} Then
          Begin
            ChangeColorset $titleWidget $warnColorset
            ChangeTitle $titleWidget {Title N/A}
          End
          Else
          Begin
            ChangeColorset $titleWidget $defaultColorset
            ChangeTitle $titleWidget $thisTitle
          End
          If $thisEMail == {empty} Then
          Begin
            ChangeColorset $emailWidget $warnColorset
            ChangeTitle $emailWidget {email N/A}
          End
          Else
          Begin
            ChangeColorset $emailWidget $emailColorset
            ChangeTitle $emailWidget $thisEMail
          End
        End
      End
      Set $titleWidget = (Add $titleWidget 3)
      Set $emailWidget = (Add $emailWidget 3)
    End
    Set $doRefresh = {false}
  End

  ##### Refresh
  If (RemainderOfDiv (GetTime) $updateTime) == 0 Then
    Set $doFetch = {true}
End

Widget		1
Property
  Type		ItemDraw
  Size		32 32
  Position		20 5
  Flags		NoFocus NoReliefString
Main
  Case message of
    SingleClic :
    Begin
      Set $doFetch = {true}
    End
  End

Widget		2
Property
  Type		ItemDraw
  Size		200 30
  Position		60 5
  Flags		NoFocus NoReliefString Left
Main
  Case message of
  End

Widget		3
Property
  Type		ItemDraw
  Size		32 32
  Position		20 45
  Flags		NoFocus NoReliefString
Main
  Case message of
  End

Widget		4
Property
  Type		ItemDraw
  Size		220 15
  Position		60 45
  Flags		NoFocus NoReliefString Left
Main
  Case message of
  End

Widget		5
Property
  Type		ItemDraw
  Size		220 15
  Position		60 60
  Flags		NoFocus NoReliefString Left
Main
  Case message of
  End

Widget		6
Property
  Type		ItemDraw
  Size		32 32
  Position		20 80
  Flags		NoFocus NoReliefString
Main
  Case message of
  End

Widget		7
Property
  Type		ItemDraw
  Size		220 15
  Position		60 80
  Flags		NoFocus NoReliefString Left
Main
  Case message of
  End

Widget		8
Property
  Type		ItemDraw
  Size		220 15
  Position		60 95
  Flags		NoFocus NoReliefString Left
Main
  Case message of
  End

Widget		9
Property
  Type		ItemDraw
  Size		32 32
  Position		20 115
  Flags		NoFocus NoReliefString
Main
  Case message of
  End

Widget		10
Property
  Type		ItemDraw
  Size		220 15
  Position		60 115
  Flags		NoFocus NoReliefString Left
Main
  Case message of
  End

Widget		11
Property
  Type		ItemDraw
  Size		220 15
  Position		60 130
  Flags		NoFocus NoReliefString Left
Main
  Case message of
  End

Widget		12
Property
  Type		ItemDraw
  Size		16 16
  Position		290 50
  Flags		NoFocus NoReliefString
Main
  Case message of
    SingleClic :
    Begin
      Set $action = {prev}
      Set $doRefresh = {true}
    End
  End

Widget		13
Property
  Type		ItemDraw
  Size		16 16
  Position		290 125
  Flags		NoFocus NoReliefString
Main
  Case message of
    SingleClic :
    Begin
      Set $action = {next}
      Set $doRefresh = {true}
    End
  End

Widget		14
Property
  Type		ItemDraw
  Size		16 16
  Position		303 1
  Flags		NoFocus NoReliefString
Main
  Case message of
    SingleClic :
    Begin
      Quit
    End
  End
